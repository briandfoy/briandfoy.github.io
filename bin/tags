#!/usr/bin/perl
use v5.10;
use strict;
use warnings;

use File::FindLib 'lib';
use File::Spec::Functions;

use ArticleUtil;

my @tags =
	grep { state %Seen; ! $Seen{$_}++ }
	map { tags_in( $_ ) }
	glob( "_posts/*.md" );

#say "tags are @tags";

my $dir = 'tag';
mkdir $dir unless -d $dir;

foreach my $tag ( @tags ) {
	my $path = catfile( $dir, "$tag.md" );
	next if -e $path;

	open my $fh, '>:utf8', $path or do {
		warn "Could not open file <$path>: $!\n";
		next;
		};

	print {$fh} <<~HERE;
		---
		layout: tagpage
		title: "Tag: $tag"
		tag: $tag
		---
		HERE
	}
